buildscript {
    repositories {
        jcenter()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.5.RELEASE")
        classpath("com.bertramlabs.plugins:asset-pipeline-gradle:2.9.1")
        classpath("com.bertramlabs.plugins:handlebars-asset-pipeline:2.6.7")
        classpath("com.eriwen:gradle-js-plugin:2.12.0")
        classpath("commons-codec:commons-codec:1.8")
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'com.bertramlabs.asset-pipeline'
apply plugin: "com.eriwen.gradle.js"

version = "1.0"

jar {
    baseName = 'seecret-app'
}

repositories {
    jcenter()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
}

dependencies {
	compile("org.springframework.boot:spring-boot-devtools")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    
}

task wrapper (type: Wrapper) {
    gradleVersion = '2.13'
}

/* Local development configuration */

sourceSets {
    dev {
        resources.srcDir file("src/assets")
    }
}

bootRun {
    systemProperties = System.properties
    main = "net.seecret.Application"
}

/* combineJs compiles all handlebars templates and other javascript into single production js file */

javascript.source {
    dev {
        js {
            srcDir "${buildDir}/assets"
            include "*.js"
        }
    }
}

processResources {
  dependsOn combineJs
}

combineJs {
    dependsOn assetCompile
    source = javascript.source.dev.js.files
    dest = file("src/main/resources/public/js/seecret_app-${version}.min.js")
}

assets {
	minifyJs = true
  	enableSourceMaps = false
    enableDigests = false
    skipNonDigests = false
    enableGzip = false
    configOptions  = [
        handlebars: [
            templateRoot: 'templates',
            templatePathSeperator: '/'
        ]
    ]
}

task "checksumPublicWebContent" {
	def path = "src/main/resources/public/"
 	def hashes = new StringBuilder()
    def sourceTree = fileTree(dir: path , includes: ["**/*.html", "**/*.js", "**/*.css"])
	sourceTree.each {File file ->
	  String checksum;
	  file.withInputStream { ins -> checksum =  org.apache.commons.codec.digest.DigestUtils.sha1Hex(ins) }
	  def webPath = relativePath(file).substring(path.size())
	  hashes.append("SHA1(").append(webPath).append(")= ").append(checksum).append("\n")
	}
	File hashesFile =file( new File("src/main/resources/public/hashes.txt"))
	hashesFile.setText(hashes.toString())
}

